// Copyright 2017 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A utility to introduce explicit loads and stores of local variables
// between instructions for stack machines such as WASM and the JVM.
class MachStackifier(gen: SsaMachGen) {
	private def parmoveDests = Vector<(VReg, List<Operand.Def>)>.new();

	private def defs = Vector<(ArchInstr, Operand.Def, bool)>.new();
	private def defStack = Vector<int>.new();
	private def loads = Vector<Operand.Use>.new();
	private def insertPoint = Vector<ArchInstr>.new();
	private def insertLoads = Vector<(ArchInstr, int, int)>.new();
	private def moveStack = Vector<int>.new();

	var out: ArchInstrBuffer;

	def stackify(uid: int, pre: ArchInstr, end: ArchInstr) {
		if (out != null) {
			out.put1("#stackify #%d {\n", uid);
			for (i = pre.next; i != end; i = i.next) out.putInstrV(i);
			out.puts("--process:");
			out.outln();
		}

		reset(pre.next);
		// Process instructions forward, tracking definitions on the abstract stack
		var start = pre.next;
		var i = start;
		while (true) {
			if (i == end) break;
			if (out != null) out.putInstrV(i);
			if (i.opcode() == ArchInstrs.ARCH_PARMOVE) {
				prepareParMove(i);
				finishInstrs(start, i);
				start = i.next;
				lowerParMove(i);
				reset(start);
				i = start;
			} else {
				processInstr(i);
				i = i.next;
			}
		}
		finishInstrs(start, end);
		setDefStackLength(0);
		if (out != null) {
			out.puts("--finish:\n");
			for (i = pre.next; i != end; i = i.next) out.putInstrV(i);
			out.puts("}");
			out.outln();
		}
	}
	def reset(next: ArchInstr) {
		setDefStackLength(0);
		loads.length = 0;
		insertPoint.length = 0;
		insertLoads.length = 0;
		insertPoint.set(0, next);
	}
	def finishInstrs(start: ArchInstr, end: ArchInstr) {
		if (out != null) out.puts("---insertLoads:\n");
		// Insert loads at points determined from previous pass
		for (j = insertLoads.length - 1; j >= 0; j--) {
			var t = insertLoads[j], next = t.0;
			for (k = t.1; k < t.2; k++) loadVRegOntoStack(loads[k].vreg, next);
		}
		loads.length = 0;
		insertLoads.length = 0;
		// Insert pops or tees for anything not consumed from stack
		if (out != null) out.puts("---finishDefs:\n");
		for (i < defs.length) finishDefs(defs[i]);
		defs.length = 0;
	}
	def processInstr(i: ArchInstr) {
		var operands = i.operands;
		var loadStart = loads.length;
		var nextHeight = defStack.length;
		var maxStackIndex = -1;
		var killIndex = operands.length;
		// Search for operands on the definition stack
		for (j < operands.length) {
			if (out != null) out.put1("\t\toperands[%d]", j);
			var o = operands[j];
			if (Operand.Kill.?(o)) {
				if (out != null) out.put1(" (kill)\n", j);
				killIndex = j;
				continue;
			}
			if (!Operand.Use.?(o)) {
				if (out != null) out.put1(" (skip)\n", j);
				continue;
			}
			var u = Operand.Use.!(o), vreg = u.vreg, ssa = vreg.ssa, s = vreg.stackIndex;
			if (out != null) out.put3(" m=%d n=%d s=%d", maxStackIndex, nextHeight, s);
			if (s < -1) {
				// vreg never seen before (e.g. constant, phi, param, in other block), cannot be on stack
				if (out != null) out.put1(" ->loads[%d]\n", loads.length - loadStart);
				loads.put(u);
				continue;
			}
			if (s <= maxStackIndex) {
				// defined before something already used from stack; load everything remaining
				if (out != null) out.puts(" not on stack\n");
				loads.put(u);
				for (k = j+1; k < operands.length; k++) {
					var o = operands[k];
					if (Operand.Use.?(o)) {
						if (out != null) out.put1("\t\toperands[%d] load\n", k);
						loads.put(Operand.Use.!(o));
					}
				}
				break;
			}
			if (out != null) out.put1(" onstack @%d\n", s);
			if (defs[defStack[s]].1.vreg != vreg) return V3.fail("def not on stackifier stack");
			insertLoadsAfter(s, loadStart);
			if (nextHeight == defStack.length) nextHeight = s;
			loadStart = loads.length;
			markLastDefAsConsumedFromStack(vreg);
			maxStackIndex = s;
		}
		insertLoadsAfter(defStack.length, loadStart);
		setDefStackLength(nextHeight);
		// Process kills
		for (i = killIndex; i < operands.length; i++) {
			var o = operands[i];
			if (!Operand.Kill.?(o)) break;
			var k = Operand.Kill.!(o);
			setDefStackLength(0); // XXX: only drop operands in kill set
		}
		// Process defs
		var next = i.next;
		for (o in operands) {
			if (!Operand.Def.?(o)) continue;
			var d = Operand.Def.!(o);
			if (out != null) out.put2("\t\tdef v%d=%d\n", d.vreg.varNum, defStack.length);
			var prev = d.vreg.stackIndex;
			if (prev >= 0) defStack[prev] = -1; // redefined; invalidate previous entry
			d.vreg.stackIndex = defStack.length;
			defs.put(i, d, false);
			defStack.put(defs.length - 1);
			insertPoint.set(defStack.length, next);
		}
		insertPoint.set(defStack.length, next);
	}
	def setDefStackLength(v: int) {
		for (i = v; i < defStack.length; i++) {
			var di = defStack[i];
			if (di >= 0) defs[di].1.vreg.stackIndex = -1;
		}
		defStack.length = v;
	}
	def markLastDefAsConsumedFromStack(vreg: VReg) {
		var i = vreg.stackIndex;
		if (i >= 0 && i < defStack.length) {
			var di = defStack[i], d = defs[di];
			defs[di] = (d.0, d.1, true); // mark as consumed from stack
		}
	}
	// Prepare to lower a parallel move. Load all move sources that are on the
	// definition stack to the move stack and mark them as consumed from the stack.
	def prepareParMove(i: ArchInstr) {
		gen.gatherParallelMoveDests(i, parmoveDests);
		moveStack.length = 0;
		for (i < defStack.length) {
			var di = defStack[i];
			if (di < 0) continue;
			var vreg = defs[di].1.vreg;
			pushOntoMoveStack(vreg, null);
		}
		setDefStackLength(0);
		if (out != null) printMoveStack();
	}
	def printMoveStack() {
		if (out == null) return;
		out.put1("moveStack |%d|: ", moveStack.length);
		for (i < moveStack.length) {
			if (i > 0) out.csp();
			var e = parmoveDests[moveStack[i]];
			out.put1("v%d ->", e.0.varNum);
			for (l = e.1; l != null; l = l.tail) {
				out.put1(" v%d", l.head.vreg.varNum);
			}
		}
		out.ln();
	}
	// Replace a parallel move with individual moves in the right order.
	// The definition stack has already been used to initialize the move stack.
	def lowerParMove(i: ArchInstr) {
		var next = i.next;
		processMoveStack(next);
		// Push any remaining move sources onto the move stack and process them
		for (i < parmoveDests.length) {
			pushOntoMoveStack(parmoveDests[i].0, next);
			processMoveStack(next);
		}
		// Lastly, remove this instruction from the block
		i.remove();
	}
	def pushOntoMoveStack(vreg: VReg, next: ArchInstr) -> bool {
		if (vreg != null && vreg.parmoveState > 0) {
			moveStack.put(vreg.parmoveState - 1);
			vreg.parmoveState = 0 - moveStack.length;
			if (next != null) loadVRegOntoStack(vreg, next);
			else markLastDefAsConsumedFromStack(vreg);
			return true;
		}
		return false;
	}
	// Use the move stack to implement a non-recursive post-order traversal of the move
	// graph. Always recurse on the first destination of the top of stack. If that
	// is already done or on the stack, generate a move, otherwise push it.
	def processMoveStack(next: ArchInstr) {
		while (moveStack.length > 0) {
			var index = moveStack[moveStack.length - 1];
			var t = parmoveDests[index], vreg = t.0, destList = t.1;
			var done = true;
			while (destList != null) {
				var dest = destList.head.vreg;
				if (pushOntoMoveStack(dest, next)) {
					done = false;
					parmoveDests[index] = (vreg, destList);  // remember remaining moves
					break;
				} else {
					var pop = destList.tail == null;  // pop if last in list
					gen.insertStoreLocal(dest, pop, next);
				}
				destList = destList.tail;
			}
			if (done) {
				moveStack.length--;     // pop off move stack
				vreg.parmoveState = 0;  // signify it's done
				parmoveDests[index] = (null, null); // defensive
			}
		}
	}
	def finishDefs(i: ArchInstr, d: Operand.Def, consumedFromStack: bool) {
		if (consumedFromStack && useCount(d) == 1) return;
		if (useCount(d) == 0) gen.insertPop(d.vreg, i.next);
		else gen.insertStoreLocal(d.vreg, !consumedFromStack, i.next);
	}
	def insertLoadsAfter(height: int, start: int) {
		var count = loads.length - start;
		if (count > 0) {
			if (out != null) out.put1("\t\tinsertLoadsAfter @%d\n", height);
			insertLoads.put(insertPoint[height], start, loads.length);
		}
	}
	def loadVRegOntoStack(vreg: VReg, next: ArchInstr) {
		if (SsaConst.?(vreg.ssa)) {
			var const = SsaConst.!(vreg.ssa);
			gen.insertLoadConst(const.vtype, const.val, next);
		} else {
			gen.insertLoadLocal(vreg, next);
		}
	}
	def useCount(d: Operand.Def) -> int {
		return d.vreg.usage.tag;
	}
}
